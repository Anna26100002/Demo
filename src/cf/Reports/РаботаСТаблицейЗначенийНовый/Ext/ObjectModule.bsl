
Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
	
	ТЗВывод = Новый ТаблицаЗначений;
	
	ТЗВывод.Колонки.Добавить("Автомобиль");
	ТЗВывод.Колонки.Добавить("ТипКузова");
	ТЗВывод.Колонки.Добавить("Производитель");
	ТЗВывод.Колонки.Добавить("Количество");
	ТЗВывод.Колонки.Добавить("Сумма");
	
	НоваяСтрока = ТЗВывод.Добавить();
	НоваяСтрока.Автомобиль = "Лада";
	НоваяСтрока.ТипКузова = "Седан";
	НоваяСтрока.Производитель = "Россия";
	НоваяСтрока.Количество = 20;
	НоваяСтрока.Сумма = 10000000;
	
	НоваяСтрока = ТЗВывод.Добавить();
	НоваяСтрока.Автомобиль = "Ситроен";
	НоваяСтрока.ТипКузова = "Хэтчбек";
	НоваяСтрока.Производитель = "Франция";
	НоваяСтрока.Количество = 5;
	НоваяСтрока.Сумма = 6000000;
	
	НоваяСтрока = ТЗВывод.Добавить();
	НоваяСтрока.Автомобиль = "Тойота";
	НоваяСтрока.ТипКузова = "Пикап";
	НоваяСтрока.Производитель = "Япония";
	НоваяСтрока.Количество = 2;
	НоваяСтрока.Сумма = 5000000;
	
	НоваяСтрока = ТЗВывод.Добавить();
	НоваяСтрока.Автомобиль = "Мицубиси";
	НоваяСтрока.ТипКузова = "Седан";
	НоваяСтрока.Производитель = "Япония";
	НоваяСтрока.Количество = 4;
	НоваяСтрока.Сумма = 4500000;
	
	// 1
	СхемаКомпоновкиДанных = Новый СхемаКомпоновкиДанных;

	ИсточникДанных = СхемаКомпоновкиДанных.ИсточникиДанных.Добавить();
	ИсточникДанных.Имя = "ИсточникДанных1";
	ИсточникДанных.ТипИсточникаДанных = "Local";

	НаборДанныхОбъектСхемыКомпоновкиДанных = СхемаКомпоновкиДанных.НаборыДанных.Добавить(Тип("НаборДанныхОбъектСхемыКомпоновкиДанных"));
	НаборДанныхОбъектСхемыКомпоновкиДанных.Имя = "НаборДанных1";
	НаборДанныхОбъектСхемыКомпоновкиДанных.ИмяОбъекта = "ТабЗнач";
	НаборДанныхОбъектСхемыКомпоновкиДанных.ИсточникДанных = "ИсточникДанных1";	
	
	ПолеНабораДанныхСхемыКомпоновкиДанных = НаборДанныхОбъектСхемыКомпоновкиДанных.Поля.Добавить(Тип("ПолеНабораДанныхСхемыКомпоновкиДанных"));
	ПолеНабораДанныхСхемыКомпоновкиДанных.Поле = "Автомобиль";
	ПолеНабораДанныхСхемыКомпоновкиДанных.ПутьКДанным = "Автомобиль";
	ПолеНабораДанныхСхемыКомпоновкиДанных.Заголовок = "Автомобиль";	       
	
	ПолеНабораДанныхСхемыКомпоновкиДанных = НаборДанныхОбъектСхемыКомпоновкиДанных.Поля.Добавить(Тип("ПолеНабораДанныхСхемыКомпоновкиДанных"));
	ПолеНабораДанныхСхемыКомпоновкиДанных.Поле = "Производитель";
	ПолеНабораДанныхСхемыКомпоновкиДанных.ПутьКДанным = "Производитель";
	ПолеНабораДанныхСхемыКомпоновкиДанных.Заголовок = "Производитель";
	
	ПолеНабораДанныхСхемыКомпоновкиДанных = НаборДанныхОбъектСхемыКомпоновкиДанных.Поля.Добавить(Тип("ПолеНабораДанныхСхемыКомпоновкиДанных"));
	ПолеНабораДанныхСхемыКомпоновкиДанных.Поле = "ТипКузова";
	ПолеНабораДанныхСхемыКомпоновкиДанных.ПутьКДанным = "ТипКузова";
	ПолеНабораДанныхСхемыКомпоновкиДанных.Заголовок = "ТипКузова";
	
	ПолеНабораДанныхСхемыКомпоновкиДанных = НаборДанныхОбъектСхемыКомпоновкиДанных.Поля.Добавить(Тип("ПолеНабораДанныхСхемыКомпоновкиДанных"));
	ПолеНабораДанныхСхемыКомпоновкиДанных.Поле = "Количество";
	ПолеНабораДанныхСхемыКомпоновкиДанных.ПутьКДанным = "Количество";
	ПолеНабораДанныхСхемыКомпоновкиДанных.Заголовок = "Количество";		
	
	ПолеНабораДанныхСхемыКомпоновкиДанных = НаборДанныхОбъектСхемыКомпоновкиДанных.Поля.Добавить(Тип("ПолеНабораДанныхСхемыКомпоновкиДанных"));
	ПолеНабораДанныхСхемыКомпоновкиДанных.Поле = "Сумма";
	ПолеНабораДанныхСхемыКомпоновкиДанных.ПутьКДанным = "Сумма";
	ПолеНабораДанныхСхемыКомпоновкиДанных.Заголовок = "Сумма";	
	
	Ресурс = СхемаКомпоновкиДанных.ПоляИтога.Добавить();
	Ресурс.ПутьКДанным = "Количество";
	Ресурс.Выражение = "Сумма(Количество)";	
	
	Ресурс = СхемаКомпоновкиДанных.ПоляИтога.Добавить();
	Ресурс.ПутьКДанным = "Сумма";
	Ресурс.Выражение = "Сумма(Сумма)";		
	
	// 2
	Настройки = КомпоновщикНастроек.ПолучитьНастройки();
	
	Если Настройки.Структура.Количество() = 0 Тогда    
		
		ПараметрыЗаголовок = Настройки.ПараметрыВывода.Элементы.Найти("Заголовок");
		ПараметрыЗаголовок.Значение = "Список автомобилей (новый)";
		ПараметрыЗаголовок.Использование = Истина;
		
		ПараметрМакетОформления = Настройки.ПараметрыВывода.Элементы.Найти("МакетОформления");
		ПараметрМакетОформления.Значение = "Море"; 
		ПараметрМакетОформления.Использование = Истина;
		
		Таблица = Настройки.Структура.Добавить(Тип("ТаблицаКомпоновкиДанных"));
		
		Группировка1 = Таблица.Строки.Добавить();
		Группировка1.Порядок.Элементы.Добавить(Тип("АвтоЭлементПорядкаКомпоновкиДанных"));
		ПолеГруппировки = Группировка1.ПоляГруппировки.Элементы.Добавить(Тип("ПолеГруппировкиКомпоновкиДанных"));
		ПолеГруппировки.Поле = Новый ПолеКомпоновкиДанных("Производитель");
		
		Группировка2 = Группировка1.Структура.Добавить(); // вложенная группировка
		Группировка2.Порядок.Элементы.Добавить(Тип("АвтоЭлементПорядкаКомпоновкиДанных"));
		ПолеГруппировки2 = Группировка2.ПоляГруппировки.Элементы.Добавить(Тип("ПолеГруппировкиКомпоновкиДанных"));
		ПолеГруппировки2.Поле = Новый ПолеКомпоновкиДанных("Автомобиль");	
		
		АвтоВыбранноеПолеКомпоновкиДанных = Группировка1.Выбор.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));
		АвтоВыбранноеПолеКомпоновкиДанных.Использование = Истина;	
		
		АвтоВыбранноеПолеКомпоновкиДанных = Группировка2.Выбор.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));
		АвтоВыбранноеПолеКомпоновкиДанных.Использование = Истина;			
		
		Колонка = Таблица.Колонки.Добавить();
		Колонка.Выбор.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));
		Колонка.Порядок.Элементы.Добавить(Тип("АвтоЭлементПорядкаКомпоновкиДанных"));
		ПолеГруппировки3 = Колонка.ПоляГруппировки.Элементы.Добавить(Тип("ПолеГруппировкиКомпоновкиДанных"));
		ПолеГруппировки3.Поле = Новый ПолеКомпоновкиДанных("ТипКузова");		                  
		
		ВыбранноеПолеКомпоновкиДанных = Настройки.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
		ВыбранноеПолеКомпоновкиДанных.Использование = Истина;
		ВыбранноеПолеКомпоновкиДанных.Поле = Новый ПолеКомпоновкиДанных("Количество");	
		
		ВыбранноеПолеКомпоновкиДанных = Настройки.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
		ВыбранноеПолеКомпоновкиДанных.Использование = Истина;
		ВыбранноеПолеКомпоновкиДанных.Поле = Новый ПолеКомпоновкиДанных("Сумма");			
		
		ЭлементПорядкаКомпоновкиДанных = Настройки.Порядок.Элементы.Добавить(Тип("ЭлементПорядкаКомпоновкиДанных"));
		ЭлементПорядкаКомпоновкиДанных.Использование = Истина;
		ЭлементПорядкаКомпоновкиДанных.Поле = Новый ПолеКомпоновкиДанных("Сумма");
		ЭлементПорядкаКомпоновкиДанных.ТипУпорядочивания = НаправлениеСортировкиКомпоновкиДанных.Убыв;		    
		
	КонецЕсли;	
	
	// 3
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, Настройки);
	
	//4
	НаборДанных = Новый Структура("ТабЗнач", ТЗВывод);
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки, НаборДанных);
	
	// 5
	ДокументРезультат.Очистить();
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);
	
КонецПроцедуры