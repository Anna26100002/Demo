
&НаКлиенте
Процедура РезультатОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка, ДополнительныеПараметры)
	
	СтандартнаяОбработка = Ложь;
	
	СтруктураРасшифровки = ПолучитьЗначениеПоляРасшифровки(Расшифровка);
	
	Если СтруктураРасшифровки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПолеРасшифровки = СтруктураРасшифровки.ИмяПоля;
	ЗначениеРасшифровки = СтруктураРасшифровки.Значение;
	
	Если ТипЗнч(ЗначениеРасшифровки) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Ключ", ЗначениеРасшифровки);
		ОткрытьФорму("Справочник.ФизическиеЛица.ФормаОбъекта", ПараметрыФормы);
		
	ИначеЕсли ПолеРасшифровки = "МестоРождения" Тогда
		
		Отбор = Новый Структура;
		Отбор.Вставить("МестоРождения", ЗначениеРасшифровки);
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("СформироватьПриОткрытии", Истина);
		ПараметрыФормы.Вставить("Отбор", Отбор);
		
		ОткрытьФорму("Отчет.ФизическиеЛица.ФормаОбъекта", ПараметрыФормы);
		
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Функция ПолучитьЗначениеПоляРасшифровки(Расшифровка)
	
	СтруктураРасшифровки = Неопределено;
	
	КоллекцияРасшифровки = ПолучитьИзВременногоХранилища(ДанныеРасшифровки);
	
	КоллекцияПолей = КоллекцияРасшифровки.Элементы[Расшифровка].ПолучитьПоля();
	
	Если КоллекцияПолей.Количество() > 0 Тогда
		СтруктураРасшифровки = Новый Структура("ИмяПоля, Значение", КоллекцияПолей[0].Поле, КоллекцияПолей[0].Значение); 
	КонецЕсли;	
	
	Возврат СтруктураРасшифровки;
	          
КонецФункции

&НаКлиенте
Процедура РезультатОбработкаДополнительнойРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка, ДополнительныеПараметры)

	СтандартнаяОбработка = Ложь;
	
	СтруктураРасшифровки = ПолучитьЗначениеПоляРасшифровки(Расшифровка);
	
	Если СтруктураРасшифровки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗначениеРасшифровки = СтруктураРасшифровки.Значение;
	
	Если ТипЗнч(ЗначениеРасшифровки) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
		
		Отбор = Новый Структура;
		Отбор.Вставить("Сотрудник", ЗначениеРасшифровки);
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Отбор", Отбор);
		ОткрытьФорму("ЖурналДокументов.КадровыеДокументы.ФормаСписка", ПараметрыФормы);
		
	КонецЕсли;
	
КонецПроцедуры
