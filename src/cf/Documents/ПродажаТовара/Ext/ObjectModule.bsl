
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ЗаказПокупателя") Тогда
		КонтактноеЛицо = ДанныеЗаполнения.КонтактноеЛицо;
		Покупатель = ДанныеЗаполнения.Покупатель;
		Для Каждого ТекСтрокаТовары Из ДанныеЗаполнения.Товары Цикл
			НоваяСтрока = Товары.Добавить();
			НоваяСтрока.Количество = ТекСтрокаТовары.Количество;
			НоваяСтрока.Номенклатура = ТекСтрокаТовары.Номенклатура;
			НоваяСтрока.Сумма = ТекСтрокаТовары.Сумма;
			НоваяСтрока.Цена = ТекСтрокаТовары.Цена;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Движения.ОстаткиТоваров.Записывать = Истина;
	Движения.ОстаткиТоваровНаСкладах.Записывать = Истина;
	Движения.Продажи.Записывать = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПродажаТовараТовары.Номенклатура КАК Номенклатура,
		|	СУММА(ПродажаТовараТовары.Количество) КАК Количество
		|ПОМЕСТИТЬ ТабДок
		|ИЗ
		|	Документ.ПродажаТовара.Товары КАК ПродажаТовараТовары
		|ГДЕ
		|	ПродажаТовараТовары.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ПродажаТовараТовары.Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТабДок.Номенклатура КАК Номенклатура,
		|	ТабДок.Количество КАК КоличествоВДокументе,
		|	ЕСТЬNULL(ТабОстСклад.КоличествоОстаток, 0) КАК КоличествоНаСкладе,
		|	ВЫБОР
		|		КОГДА ТабОстВсе.КоличествоОстаток ЕСТЬ NULL
		|			ТОГДА 0
		|		ИНАЧЕ ТабДок.Количество / ТабОстВсе.КоличествоОстаток * ТабОстВсе.СтоимостьОстаток
		|	КОНЕЦ КАК СтоимостьПоСреднемуПоФирме
		|ИЗ
		|	ТабДок КАК ТабДок
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиТоваровНаСкладах.Остатки(
		|				&МВ,
		|				Номенклатура В
		|						(ВЫБРАТЬ
		|							ТабДок.Номенклатура КАК Номенклатура
		|						ИЗ
		|							ТабДок КАК ТабДок)
		|					И Склад = &Склад) КАК ТабОстСклад
		|		ПО ТабДок.Номенклатура = ТабОстСклад.Номенклатура
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиТоваров.Остатки(
		|				&МВ,
		|				Номенклатура В
		|					(ВЫБРАТЬ
		|						ТабДок.Номенклатура КАК Номенклатура
		|					ИЗ
		|						ТабДок КАК ТабДок)) КАК ТабОстВсе
		|		ПО ТабДок.Номенклатура = ТабОстВсе.Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ТабДок";
	
	Запрос.УстановитьПараметр("МВ", МоментВремени());
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Склад", Склад);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		Если ВыборкаДетальныеЗаписи.КоличествоВДокументе > ВыборкаДетальныеЗаписи.КоличествоНаСкладе Тогда
			
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Не хватает товара: " +
			ВыборкаДетальныеЗаписи.Номенклатура + Символы.ПС + "Нехватка составляет: " +
			(ВыборкаДетальныеЗаписи.КоличествоВДокументе - ВыборкаДетальныеЗаписи.КоличествоНаСкладе);
			Сообщение.Поле = "Склад";
			Сообщение.УстановитьДанные(ЭтотОбъект);
			Сообщение.Сообщить();
			Отказ = Истина;
			
		КонецЕсли;
		
		Если НЕ Отказ Тогда
			
			Движение = Движения.ОстаткиТоваров.ДобавитьРасход();
			Движение.Период = Дата;
			Движение.Номенклатура = ВыборкаДетальныеЗаписи.Номенклатура;
			Движение.Количество = ВыборкаДетальныеЗаписи.КоличествоВДокументе;
			Движение.Стоимость = ВыборкаДетальныеЗаписи.СтоимостьПоСреднемуПоФирме;
			
			Движение = Движения.ОстаткиТоваровНаСкладах.ДобавитьРасход();
			Движение.Период = Дата;
			Движение.Номенклатура = ВыборкаДетальныеЗаписи.Номенклатура;
			Движение.Склад = Склад;
			Движение.Количество = ВыборкаДетальныеЗаписи.КоличествоВДокументе;
			
		КонецЕсли;		
		
	КонецЦикла;
	
	Если НЕ Отказ Тогда
	
		Для каждого СтрокаТЧ Из Товары Цикл
			Движение = Движения.Продажи.Добавить();
			Движение.Период = Дата;
			Движение.Номенклатура = СтрокаТЧ.Номенклатура;
			Движение.Покупатель = Покупатель;
			Движение.Количество = СтрокаТЧ.Количество;
			Движение.Сумма = СтрокаТЧ.Сумма;
		КонецЦикла;
		
	КонецЕсли;	
		
КонецПроцедуры
